cmake_minimum_required(VERSION 3.10)
project(TGFX)

#set(CMAKE_VERBOSE_MAKEFILE ON)
include(./third_party/vendor_tools/vendor.cmake)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
cmake_policy(SET CMP0063 NEW)
set(CMAKE_CXX_VISIBILITY_PRESET hidden)

# Options for building tgfx
option(TGFX_USE_OPENGL "Allow use of OpenGL as GPU backend" ON)
option(TGFX_USE_QT "Allow build with QT frameworks." OFF)
option(TGFX_USE_SWIFTSHADER "Allow build with SwiftShader library" OFF)
option(TGFX_BUILD_DRAWERS "Build the tgfx-drawers library" OFF)

if (APPLE OR WEB)
    option(TGFX_USE_FREETYPE "Allow use of embedded freetype library" OFF)
else ()
    option(TGFX_USE_FREETYPE "Allow use of embedded freetype library" ON)
endif ()

if (IOS OR WEB)
    option(TGFX_USE_WEBP_DECODE "Enable embedded WEBP decoding support" ON)
elseif (NOT ANDROID)
    option(TGFX_USE_PNG_DECODE "Enable embedded PNG decoding support" ON)
    option(TGFX_USE_PNG_ENCODE "Enable embedded PNG encoding support" ON)
    option(TGFX_USE_JPEG_DECODE "Enable embedded JPEG decoding support" ON)
    option(TGFX_USE_JPEG_ENCODE "Enable embedded JPEG encoding support" ON)
    option(TGFX_USE_WEBP_DECODE "Enable embedded WEBP decoding support" ON)
    option(TGFX_USE_WEBP_ENCODE "Enable embedded WEBP encoding support" ON)
endif ()

string(FIND ${CMAKE_COMMAND} "/CLion.app/" CLionPosition)
get_directory_property(HasParent PARENT_DIRECTORY)
if (MACOS AND CLionPosition GREATER -1 AND NOT HasParent)
    # CLion project needs test targets.
    option(TGFX_BUILD_TESTS "Build tgfx tests" ON)
endif ()

if (TGFX_BUILD_TESTS)
    set(TGFX_USE_FREETYPE ON)
    set(TGFX_BUILD_DRAWERS ON)
else ()
    set(TGFX_BUILD_TESTS OFF)
endif ()

message("TGFX_USE_OPENGL: ${TGFX_USE_OPENGL}")
message("TGFX_USE_QT: ${TGFX_USE_QT}")
message("TGFX_USE_SWIFTSHADER: ${TGFX_USE_SWIFTSHADER}")
message("TGFX_USE_FREETYPE: ${TGFX_USE_FREETYPE}")
message("TGFX_USE_PNG_DECODE: ${TGFX_USE_PNG_DECODE}")
message("TGFX_USE_PNG_ENCODE: ${TGFX_USE_PNG_ENCODE}")
message("TGFX_USE_JPEG_DECODE: ${TGFX_USE_JPEG_DECODE}")
message("TGFX_USE_JPEG_ENCODE: ${TGFX_USE_JPEG_ENCODE}")
message("TGFX_USE_WEBP_DECODE: ${TGFX_USE_WEBP_DECODE}")
message("TGFX_USE_WEBP_ENCODE: ${TGFX_USE_WEBP_ENCODE}")
message("TGFX_BUILD_TESTS: ${TGFX_BUILD_TESTS}")

if (NOT CMAKE_OSX_DEPLOYMENT_TARGET)
    if (DEPLOYMENT_TARGET)
        set(CMAKE_OSX_DEPLOYMENT_TARGET ${DEPLOYMENT_TARGET})
    elseif (MACOS)
        if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
            set(CMAKE_OSX_DEPLOYMENT_TARGET "11.0")
        else ()
            set(CMAKE_OSX_DEPLOYMENT_TARGET "10.15")
        endif ()
    elseif (IOS)
        set(CMAKE_OSX_DEPLOYMENT_TARGET "9.0")
    endif ()
endif ()

# collects tgfx common files.
list(APPEND TGFX_INCLUDES src)
file(GLOB_RECURSE SRC_FILES
        src/core/*.*
        src/effects/*.*
        src/images/*.*
        src/shaders/*.*
        src/shapes/*.*
        src/utils/*.*
        src/gpu/*.*)
list(APPEND TGFX_FILES ${SRC_FILES})
file(GLOB PLATFORM_COMMON_FILES
        src/codecs/*.*
        src/vectors/*.*
        src/platform/*.*)
list(APPEND TGFX_FILES ${PLATFORM_COMMON_FILES})

if (CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    list(APPEND TGFX_COMPILE_OPTIONS -Werror -Wall -Wextra -Weffc++ -pedantic -Werror=return-type)
endif ()

if (MSVC)
    list(APPEND TGFX_COMPILE_OPTIONS "/utf-8")
    list(APPEND TGFX_COMPILE_OPTIONS /w44251 /w44275)
    list(APPEND TGFX_DEFINES NOMINMAX _USE_MATH_DEFINES)
endif (MSVC)

# Sets flags
if (CMAKE_BUILD_TYPE MATCHES "Debug")
    list(APPEND TGFX_DEFINES DEBUG)
    #set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fsanitize=address -fno-omit-frame-pointer -g -O1")
endif ()

if (TGFX_USE_QT)
    set(TGFX_USE_SWIFTSHADER OFF)
endif ()

if (TGFX_USE_QT OR TGFX_USE_SWIFTSHADER)
    set(TGFX_USE_OPENGL ON)
endif ()

if (TGFX_USE_FREETYPE)
    # Freetype needs libpng
    set(TGFX_USE_PNG_DECODE ON)
endif ()

if (TGFX_USE_PNG_DECODE)
    list(APPEND TGFX_DEFINES TGFX_USE_PNG_DECODE)
    set(TGFX_USE_PNG ON)
endif ()

if (TGFX_USE_PNG_ENCODE)
    list(APPEND TGFX_DEFINES TGFX_USE_PNG_ENCODE)
    set(TGFX_USE_PNG ON)
endif ()

if (TGFX_USE_JPEG_DECODE)
    list(APPEND TGFX_DEFINES TGFX_USE_JPEG_DECODE)
    set(TGFX_USE_JPEG ON)
endif ()

if (TGFX_USE_JPEG_ENCODE)
    list(APPEND TGFX_DEFINES TGFX_USE_JPEG_ENCODE)
    set(TGFX_USE_JPEG ON)
endif ()

if (TGFX_USE_WEBP_DECODE)
    list(APPEND TGFX_DEFINES TGFX_USE_WEBP_DECODE)
    set(TGFX_USE_WEBP ON)
endif ()

if (TGFX_USE_WEBP_ENCODE)
    list(APPEND TGFX_DEFINES TGFX_USE_WEBP_ENCODE)
    set(TGFX_USE_WEBP ON)
endif ()

if (TGFX_USE_JPEG)
    list(APPEND TGFX_STATIC_VENDORS libjpeg-turbo)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/codecs/jpeg/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    list(APPEND TGFX_INCLUDES third_party/out/libjpeg-turbo/${INCLUDE_ENTRY})
endif ()

if (TGFX_USE_WEBP)
    list(APPEND TGFX_STATIC_VENDORS libwebp)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/codecs/webp/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    list(APPEND TGFX_INCLUDES third_party/libwebp/src)
endif ()

if (TGFX_USE_PNG)
    set(TGFX_USE_ZLIB ON)
    list(APPEND TGFX_STATIC_VENDORS libpng)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/codecs/png/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    list(APPEND TGFX_INCLUDES third_party/out/libpng/${INCLUDE_ENTRY})
endif ()

if (TGFX_USE_ZLIB)
    list(APPEND TGFX_STATIC_VENDORS zlib)
endif ()

if (TGFX_USE_FREETYPE)
    list(APPEND TGFX_DEFINES TGFX_USE_FREETYPE)
    list(APPEND TGFX_STATIC_VENDORS freetype)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/vectors/freetype/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    list(APPEND TGFX_INCLUDES third_party/freetype/include)
elseif (WEB)
    list(APPEND TGFX_DEFINES TGFX_BUILD_FOR_WEB)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/vectors/web/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
elseif (APPLE)
    # Uses CoreGraphics instead.
    list(APPEND TGFX_DEFINES TGFX_USE_CORE_GRAPHICS)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/vectors/coregraphics/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
endif ()

if (TGFX_USE_OPENGL)
    file(GLOB GFX_PLATFORM_FILES src/opengl/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    file(GLOB_RECURSE GFX_PROCESSOR_FILES src/opengl/processors/*.*)
    list(APPEND TGFX_FILES ${GFX_PROCESSOR_FILES})
elseif (ANDROID)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/vulkan/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
elseif (APPLE)
    file(GLOB_RECURSE GFX_PLATFORM_FILES src/metal/*.*)
    list(APPEND TGFX_FILES ${GFX_PLATFORM_FILES})
    find_library(Metal_LIBS Metal REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${Metal_LIBS})
endif ()

file(GLOB_RECURSE EGL_PLATFORM_FILES src/opengl/egl/*.*)

if (TGFX_USE_QT)
    # need to set the CMAKE_PREFIX_PATH to local QT installation path, for example :
    # set(CMAKE_PREFIX_PATH /Users/username/Qt5.13.0/5.13.0/clang_64/lib/cmake)
    find_package(Qt5OpenGL REQUIRED)
    find_package(Qt5Gui REQUIRED)
    find_package(Qt5Quick REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${Qt5OpenGL_LIBRARIES} ${Qt5Gui_LIBRARIES} ${Qt5Quick_LIBRARIES})
    list(APPEND TGFX_INCLUDES ${Qt5OpenGL_INCLUDE_DIRS} ${Qt5Gui_INCLUDE_DIRS} ${Qt5Quick_INCLUDE_DIRS})
    file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/qt/*.*)
    list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    if (MACOS)
        file(GLOB CGL_PLATFORM_FILES src/opengl/cgl/CGLHardwareTexture.mm)
        list(APPEND TGFX_FILES ${CGL_PLATFORM_FILES})
        list(APPEND TGFX_DEFINES GL_SILENCE_DEPRECATION)
        find_library(OpenGL_LIBS OpenGL REQUIRED)
        list(APPEND TGFX_SHARED_LIBS ${OpenGL_LIBS})
    endif ()
elseif (TGFX_USE_SWIFTSHADER)
    list(APPEND TGFX_INCLUDES vendor/swiftshader/include)
    file(GLOB SWIFTSHADER_LIBS vendor/swiftshader/${LIBRARY_ENTRY}/*${CMAKE_SHARED_LIBRARY_SUFFIX})
    list(APPEND TGFX_SHARED_LIBS ${SWIFTSHADER_LIBS})
    list(APPEND TGFX_FILES ${EGL_PLATFORM_FILES})
else ()
    set(TGFX_USE_NATIVE_GL ON)
endif ()

list(APPEND TGFX_STATIC_VENDORS pathkit)
list(APPEND TGFX_INCLUDES third_party/pathkit)
list(APPEND TGFX_STATIC_VENDORS skcms)
list(APPEND TGFX_INCLUDES third_party/skcms)

if (WEB)
    file(GLOB_RECURSE PLATFORM_FILES src/platform/web/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})
    if (TGFX_USE_NATIVE_GL)
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/webgl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()
elseif (IOS)
    # finds all required platform libraries.
    find_library(UIKit_LIBS UIKit REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${UIKit_LIBS})
    find_library(Foundation_LIBS Foundation REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${Foundation_LIBS})
    find_library(QuartzCore_LIBS QuartzCore REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${QuartzCore_LIBS})
    find_library(CoreGraphics_LIBS CoreGraphics REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${CoreGraphics_LIBS})
    find_library(CoreText_LIBS CoreText REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${CoreText_LIBS})
    find_library(CoreMedia_LIBS CoreMedia REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${CoreMedia_LIBS})
    find_library(CoreMedia_LIBS CoreMedia REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${CoreMedia_LIBS})
    find_library(ImageIO_LIBS ImageIO REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${ImageIO_LIBS})
    find_library(CORE_VIDEO CoreVideo)
    list(APPEND TGFX_SHARED_LIBS ${CORE_VIDEO})
    find_library(ICONV_LIBRARIES NAMES iconv libiconv libiconv-2 c)
    list(APPEND TGFX_SHARED_LIBS ${ICONV_LIBRARIES})

    file(GLOB_RECURSE PLATFORM_FILES src/platform/apple/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})

    if (TGFX_USE_NATIVE_GL)
        list(APPEND TGFX_DEFINES GLES_SILENCE_DEPRECATION)
        find_library(OpenGLES_LIBS OpenGLES REQUIRED)
        list(APPEND TGFX_SHARED_LIBS ${OpenGLES_LIBS})
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/eagl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()
elseif (MACOS)
    # finds all required platform libraries.
    find_library(APPLICATION_SERVICES_FRAMEWORK ApplicationServices REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${APPLICATION_SERVICES_FRAMEWORK})
    find_library(QUARTZ_CORE QuartzCore REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${QUARTZ_CORE})
    find_library(COCOA Cocoa REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${COCOA})
    find_library(FOUNDATION Foundation REQUIRED)
    list(APPEND TGFX_SHARED_LIBS ${FOUNDATION})
    find_library(ICONV_LIBRARIES NAMES iconv libiconv libiconv-2 c)
    list(APPEND TGFX_SHARED_LIBS ${ICONV_LIBRARIES})
    find_library(CORE_MEDIA CoreMedia)
    list(APPEND TGFX_SHARED_LIBS ${CORE_MEDIA})

    file(GLOB_RECURSE PLATFORM_FILES src/platform/apple/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})

    if (TGFX_USE_NATIVE_GL)
        list(APPEND TGFX_DEFINES GL_SILENCE_DEPRECATION)
        find_library(OpenGL_LIBS OpenGL REQUIRED)
        list(APPEND TGFX_SHARED_LIBS ${OpenGL_LIBS})
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/cgl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()

elseif (ANDROID)

    find_library(LOG_LIB log)
    list(APPEND TGFX_SHARED_LIBS ${LOG_LIB})
    find_library(ANDROID_LIB android)
    list(APPEND TGFX_SHARED_LIBS ${ANDROID_LIB})
    find_library(JNIGRAPHICS_LIB jnigraphics)
    list(APPEND TGFX_SHARED_LIBS ${JNIGRAPHICS_LIB})

    file(GLOB_RECURSE PLATFORM_FILES src/platform/android/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})

    if (TGFX_USE_NATIVE_GL)
        find_library(GLESV2_LIB GLESv2)
        list(APPEND TGFX_SHARED_LIBS ${GLESV2_LIB})
        find_library(EGL_LIB EGL)
        list(APPEND TGFX_SHARED_LIBS ${EGL_LIB})
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/egl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()

    # optimizes the output size
    list(APPEND TGFX_COMPILE_OPTIONS -ffunction-sections -fdata-sections -Os -fno-exceptions -fno-rtti)

elseif (WIN32)
    find_library(Bcrypt_LIB Bcrypt)
    list(APPEND TGFX_SHARED_LIBS ${Bcrypt_LIB})
    find_library(ws2_32_LIB ws2_32)
    list(APPEND TGFX_SHARED_LIBS ${ws2_32_LIB})
    find_library(DWrite_LIB DWrite)
    list(APPEND TGFX_STATIC_LIBS ${DWrite_LIB})

    file(GLOB_RECURSE PLATFORM_FILES src/platform/mock/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})

    if (TGFX_USE_NATIVE_GL)
        file(GLOB ANGLE_LIBS vendor/angle/${PLATFORM}/${ARCH}/*${CMAKE_STATIC_LIBRARY_SUFFIX})
        list(APPEND GPU_PLATFORM_STATIC_LIBS ${ANGLE_LIBS})
        list(APPEND TGFX_INCLUDES vendor/angle/include)
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/egl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()
elseif (CMAKE_HOST_SYSTEM_NAME MATCHES "Linux")
    find_package(Threads)
    list(APPEND TGFX_SHARED_LIBS ${CMAKE_THREAD_LIBS_INIT})
    list(APPEND TGFX_SHARED_LIBS dl)
    list(APPEND TGFX_COMPILE_OPTIONS -fPIC -pthread)

    file(GLOB_RECURSE PLATFORM_FILES src/platform/mock/*.*)
    list(APPEND TGFX_FILES ${PLATFORM_FILES})

    if (TGFX_USE_NATIVE_GL)
        find_library(GLESV2_LIB GLESv2)
        list(APPEND TGFX_SHARED_LIBS ${GLESV2_LIB})
        find_library(EGL_LIB EGL)
        list(APPEND TGFX_SHARED_LIBS ${EGL_LIB})
        file(GLOB_RECURSE GPU_PLATFORM_FILES src/opengl/egl/*.*)
        list(APPEND TGFX_FILES ${GPU_PLATFORM_FILES})
    endif ()
endif ()

add_vendor_target(TGFX STATIC_VENDORS ${TGFX_STATIC_VENDORS} SHARED_VENDORS ${TGFX_SHARED_VENDORS})
list(APPEND TGFX_STATIC_LIBS ${TGFX_VENDOR_STATIC_LIBRARIES})
list(APPEND TGFX_SHARED_LIBS ${TGFX_VENDOR_SHARED_LIBRARIES})
list(APPEND TGFX_VENDOR_TARGETS ${TGFX_VENDOR_TARGET})

if (HasParent)
    set(TGFX_STATIC_LIBS ${TGFX_STATIC_LIBS} PARENT_SCOPE)
    set(TGFX_SHARED_LIBS ${TGFX_SHARED_LIBS} PARENT_SCOPE)
endif ()

foreach (FILE_PATH ${TGFX_FILES})
    get_filename_component(FILE_NAME "${FILE_PATH}" NAME_WE)
    if (NOT FILE_NAME)
        list(APPEND TGFX_EXCLUDE_FILES ${FILE_PATH})
    endif ()
endforeach ()

if (TGFX_EXCLUDE_FILES)
    list(REMOVE_ITEM TGFX_FILES ${TGFX_EXCLUDE_FILES})
endif ()

add_library(tgfx STATIC ${TGFX_VENDOR_TARGETS} ${TGFX_FILES})
target_compile_definitions(tgfx PUBLIC ${TGFX_DEFINES})
target_compile_options(tgfx PUBLIC ${TGFX_COMPILE_OPTIONS})
target_include_directories(tgfx PUBLIC include PRIVATE ${TGFX_INCLUDES})
merge_libraries_into(tgfx ${TGFX_STATIC_LIBS})
target_link_libraries(tgfx ${TGFX_SHARED_LIBS})
set_target_properties(tgfx PROPERTIES PREFIX "")

if (TGFX_BUILD_DRAWERS)
    file(GLOB_RECURSE DRAWER_FILES drawers/src/*.*)
    add_library(tgfx-drawers STATIC ${DRAWER_FILES})
    set_target_properties(tgfx-drawers PROPERTIES PREFIX "")
    target_include_directories(tgfx-drawers PUBLIC drawers/include PRIVATE include drawers/src)
    target_link_libraries(tgfx-drawers tgfx)
endif ()

if (TGFX_BUILD_TESTS)
    execute_process(COMMAND git rev-parse --short HEAD OUTPUT_VARIABLE HEAD_COMMIT)
    file(WRITE ${CMAKE_CURRENT_SOURCE_DIR}/test/baseline/.cache/HEAD "${HEAD_COMMIT}")
    if (NOT TGFX_SKIP_BASELINE_CHECK)
        set(CacheVersionFile ${CMAKE_CURRENT_SOURCE_DIR}/test/baseline/.cache/version.json)
        if (EXISTS ${CacheVersionFile})
            execute_process(COMMAND git diff -G. --name-only origin/main:test/baseline/version.json ${CacheVersionFile} OUTPUT_VARIABLE HAS_DIFF)
        else ()
            set(HAS_DIFF ON)
        endif ()
        if (HAS_DIFF)
            message(WARNING "The local md5 cache is out of date! Please run the 'update_baseline.sh' to regenerate the cache.")
        endif ()
    endif ()

    file(GLOB_RECURSE TGFX_TEST_FILES test/src/*.*)
    list(APPEND TGFX_TEST_LIBS tgfx tgfx-drawers ${TGFX_SHARED_LIBS})
    list(APPEND TGFX_TEST_INCLUDES ${TGFX_INCLUDES} test/src third_party/json/include)

    add_vendor_target(TEST STATIC_VENDORS harfbuzz googletest)
    list(APPEND TGFX_TEST_LIBS ${TEST_VENDOR_STATIC_LIBRARIES})
    list(APPEND TGFX_TEST_VENDOR_TARGETS ${TEST_VENDOR_TARGET})
    list(APPEND TGFX_TEST_INCLUDES
            third_party/harfbuzz/src
            third_party/googletest/googletest
            third_party/googletest/googletest/include)

    list(APPEND TGFX_TEST_COMPILE_OPTIONS ${TGFX_COMPILE_OPTIONS} -fno-access-control)
    list(APPEND TGFX_TEST_DEFINES ${TGFX_DEFINES})

    foreach (FILE_PATH ${TGFX_TEST_FILES})
        get_filename_component(FILE_NAME "${FILE_PATH}" NAME_WE)
        if (NOT FILE_NAME)
            list(APPEND TGFX_TEST_EXCLUDE_FILES ${FILE_PATH})
        endif ()
    endforeach ()

    if (TGFX_TEST_EXCLUDE_FILES)
        list(REMOVE_ITEM TGFX_TEST_FILES ${TGFX_TEST_EXCLUDE_FILES})
    endif ()

    # used to update the local md5 data for baseline testing.
    add_executable(UpdateBaseline ${TGFX_TEST_VENDOR_TARGETS} ${TGFX_TEST_FILES})
    target_include_directories(UpdateBaseline PUBLIC ${TGFX_TEST_INCLUDES})
    target_compile_definitions(UpdateBaseline PUBLIC ${TGFX_TEST_DEFINES} UPDATE_BASELINE)
    target_compile_options(UpdateBaseline PUBLIC ${TGFX_TEST_COMPILE_OPTIONS})
    target_link_options(UpdateBaseline PUBLIC ${TGFX_TEST_LINK_OPTIONS})
    target_link_libraries(UpdateBaseline ${TGFX_TEST_LIBS})

    # used to generate baseline images to the out/ directory. each image has a "_base" suffix.
    add_executable(GenerateImages ${TGFX_TEST_VENDOR_TARGETS} ${TGFX_TEST_FILES})
    target_include_directories(GenerateImages PUBLIC ${TGFX_TEST_INCLUDES})
    target_compile_definitions(GenerateImages PUBLIC ${TGFX_TEST_DEFINES} GENERATOR_BASELINE_IMAGES)
    target_compile_options(GenerateImages PUBLIC ${TGFX_TEST_COMPILE_OPTIONS})
    target_link_options(GenerateImages PUBLIC ${TGFX_TEST_LINK_OPTIONS})
    target_link_libraries(GenerateImages ${TGFX_TEST_LIBS})

    add_executable(TGFXUnitTest ${TGFX_TEST_VENDOR_TARGETS} ${TGFX_TEST_FILES})
    target_include_directories(TGFXUnitTest PUBLIC ${TGFX_TEST_INCLUDES})
    target_compile_definitions(TGFXUnitTest PUBLIC ${TGFX_TEST_DEFINES} SKIP_FRAME_COMPARE)
    target_compile_options(TGFXUnitTest PUBLIC ${TGFX_TEST_COMPILE_OPTIONS})
    target_link_options(TGFXUnitTest PUBLIC ${TGFX_TEST_LINK_OPTIONS})
    target_link_libraries(TGFXUnitTest ${TGFX_TEST_LIBS})

    add_executable(TGFXFullTest ${TGFX_TEST_VENDOR_TARGETS} ${TGFX_TEST_FILES})
    target_compile_definitions(TGFXFullTest PUBLIC ${TGFX_TEST_DEFINES})
    target_compile_options(TGFXFullTest PUBLIC ${TGFX_TEST_COMPILE_OPTIONS})
    target_include_directories(TGFXFullTest PUBLIC ${TGFX_TEST_INCLUDES})
    target_link_options(TGFXFullTest PUBLIC ${TGFX_TEST_LINK_OPTIONS})
    target_link_libraries(TGFXFullTest ${TGFX_TEST_LIBS})
endif ()
